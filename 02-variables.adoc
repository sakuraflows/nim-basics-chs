= 命名值(Naming values)



在编程中，为程序中的值赋予名称通常有助于我们追踪和管理数据。
例如，当向用户询问其姓名时，我们希望存储该姓名以便后续使用，避免每次需要进行相关计算时反复询问。

在示例 `pi = 3.14` 中，名称 `pi` 与值 `3.14` 相关联。
根据经验可知，变量 `pi` 的类型为（十进制）数字。

Another example would be `firstName = Alice`, where `firstName` is the name of a variable with the value `Alice`.
We would say that the type of this variable is a word.

编程语言中的机制与此类似。
这些名称分配包含它们的名称(_name_)、值(_value_)和类型(_type_)三个要素。




== Variable declaration


Nim 是一种静态类型( https://en.wikipedia.org/wiki/Type_system#STATIC[statically typed] )编程语言，这意味着在使用值之前需要声明其赋值类型。（译者注：其实nim也可以自动推理，有的情况下无需声明。）

在 Nim 中，我们还会区分可以改变或可变的值与不可变的值(mutate)，但这一点稍后再详述。

我们可以使用 `var` 关键字声明一个变量（即可变赋值），只需通过以下语法指定其名称和类型（值可以稍后添加）：
[source]
----
var <name>: <type>
----

如果我们已经知道它的值，可以立即声明一个变量并为其赋值：
[source]
----
var <name>: <type> = <value>
----
NOTE: 尖括号（ `<>` ）用于表示可以更改的内容. +
所以 `<name>` 并非字面意义上的尖括号内的单词 `name` ，而是指代任意名称。


Nim 还具备类型推断( https://en.wikipedia.org/wiki/Type_inference[type inference] )能力：编译器能根据赋值自动推断变量类型，无需显式声明。
我们将在下一章深入探讨各种类型。

因此我们可以像这样不指定显式类型来声明变量:
[source]
----
var <name> = <value>
----


在 Nim 中，一个示例如下:
[source]
----
var a: int  # <1>
var b = 7   # <2>
----
<1> 变量 `a` 的类型为 `int` （整数），未显式设置值。(with no value explicitly set.)
<2> 变量 `b` 的值为 `7` ，其类型被自动推断为整数。


在命名时，选择对程序有意义的名称非常重要。
简单地命名为 `a` 、 `b` 、 `c` 等很快就会变得混乱。
名称中不能使用空格，因为这会将其分割成两部分。
因此，如果你选择的名称由多个单词组成，通常的做法是使用 `camelCase` 风格书写（注意名称的首字母应为小写）。

但请注意，Nim 对大小写和下划线不敏感，这意味着 `helloWorld` 和 `hello_world` 会被视为相同的名称。
唯一的例外是首字符，它对大小写敏感。
名称中还可以包含数字和其他 UTF-8 字符，甚至可以使用表情符号（如果你愿意的话），但请记住，你和其他人可能需要输入这些字符。

{nbsp}

无需为每个变量单独输入` var `，可以在同一个` var `块中声明多个变量（类型不必相同）。
在 Nim 中，块是指具有相同缩进（首字符前空格数相同）的代码部分，默认缩进级别为两个空格。
在 Nim 程序中，此类块随处可见，不仅用于命名赋值。

[source]
----
var
  c = -11
  d = "Hello"
  e = '!'
----

NOTE: 在 Nim 中不允许使用制表符进行缩进. +
你可以设置代码编辑器将按下 Tab 转换为任意数量的空格. +
在 VS Code 中，默认设置是将 `Tab` 转换为四个空格。
这可以通过在设置中（ `Ctrl+,` ）配置 `"editor.tabSize": 2` 轻松覆盖。


如前所述，变量是可变的，即:它们的值可以（多次）改变，但变量的类型必须与声明时保持一致。

[source]
----
var f = 7           <1>

f = -3              <2>
f = 19
f = "Hello" # error <3><4>
----
<1> 变量 `f` 的初始值为 `7` ，其类型被推断为 `int` 。
<2> 首先将 `f` 的值更改为 `-3` ，然后再改为 `19` 。这两个值都是整数，与原值类型相同。
<3> Trying to change the value of `f` to `"Hello"` produces an error because `Hello` is not a number, and this would change the type of `f` from an integer to a string.
<4> `# error` is a comment. Comments in Nim code are written after a `#` character. Everything after it on the same line will be ignored.




== Immutable assignment

Unlike variables declared with `var` keyword, two more types of assignment exist in Nim, whose value cannot change, one declared with the `const` keyword, and the other declared with the `let` keyword.



=== Const

The value of an immutable assignment declared with `const` keyword must be known at compile time (before the program is run).

For example, we can declare the acceleration of gravity as `const g = 9.81` or pi as `const pi = 3.14`, as we know their values in advance and these values will not change during the execution of our program.

[source]
----
const g = 35
g = -27         # error <1>

var h = -5
const i = h + 7 # error <2>
----
<1> The value of a constant cannot be changed.
<2> Variable `h` is not evaluated at compile time (it is a variable and its value can change during the execution of a program), consequently the value of constant `i` can't be known at compile time, and this will raise an error.

In some programming languages it is a common practice to have the names of constants written in `ALL_CAPS`.
Constants in Nim are written just like any other variable.



=== Let

Immutable assignments declared with `let` don't need to be known at compile time, their value can be set at any time during the execution of a program, but once it is set, their value cannot change.

[source]
----
let j = 35
j = -27 # error <1>

var k = -5
let l = k + 7   <2>
----
<1> The value of an immutable cannot be changed.
<2> In contrast to the `const` example above, this works.

In practice, you will see/use `let` more frequently than `const`.

While you could use `var` for everything, your default choice should be `let`.
Use `var` only for the variables which will be modified.
